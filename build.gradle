buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
        classpath "com.palantir:jacoco-coverage:0.4.0"
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.palantir.jacoco-full-report"

    group 'com.feedsome'

    sourceCompatibility = "${javaVersion}"
    targetCompatibility = "${javaVersion}"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    //
    // Maven like dependency management
    //
    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-netflix:1.2.3.RELEASE'
        }
        dependencies {
            dependency 'junit:junit:4.12'
            dependency 'org.mockito:mockito-core:1.10.19'
        }
    }

}

subprojects {
    apply plugin: "org.springframework.boot"

    springBoot {
        backupSource = false
    }

    bootRepackage {
        enabled = false
    }

    //
    // Dependencies
    //
    dependencies {
        //  Spring Dependencies
        compile('org.springframework.boot:spring-boot-starter-web') {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
        }

        compile "org.springframework.boot:spring-boot-starter-undertow"
        compile "org.springframework.boot:spring-boot-starter-actuator"

        compile 'org.springframework.cloud:spring-cloud-starter-eureka'


        //  3rd Party Dependencies
        compile "org.jolokia:jolokia-core"

        //  Jackson Dependencies
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"

        // Test dependencies
        testCompile 'junit:junit'
        testCompile 'org.mockito:mockito-core'
    }
}

//
// Task declaration
//

task wrapper(type: Wrapper) {
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
